apply plugin: 'com.google.gms.google-services'

buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'io.fabric.tools:gradle:1.25.4'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.github.triplet.play'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

repositories {
    maven { url "https://maven.google.com" }
    maven { url 'https://maven.fabric.io/public' }
    mavenCentral()
}
//https://stackoverflow.com/a/50922399/2096828
//com.google.gms.googleservices.GoogleServicesPlugin.config.disableVersionCheck = true

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        configVerdinho {
            storeFile file('/Users/victor/OneDrive/Java/verdinho/verdinho.jks')
            storePassword "${VERDINHO_STORE_PASS}"
            keyAlias 'verdinho'
            keyPassword "${VERDINHO_KEY_PASS}"
        }
        configTranscol {
            storeFile file('/Users/victor/OneDrive/Java/verdinho/verdinho.jks')
            storePassword "${VERDINHO_STORE_PASS}"
            keyAlias 'meutranscol'
            keyPassword "${VERDINHO_KEY_PASS}"
        }
        configBrasilia {
            storeFile file('/Users/victor/OneDrive/Java/verdinho/verdinho.jks')
            storePassword "${VERDINHO_STORE_PASS}"
            keyAlias 'brasilia'
            keyPassword "${VERDINHO_KEY_PASS}"
        }
    }
    playAccountConfigs {
        defaultAccountConfig {
            jsonFile = file('/Users/victor/OneDrive/Java/verdinho/Verdinho-playAccount.json')
        }
    }
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 40
        versionName "1.12.3"
        multiDexEnabled true
        playAccountConfig = playAccountConfigs.defaultAccountConfig
        buildConfigField("String", "TWITTER_CONSUMER_KEYS", "\"" + TWITTER_CONSUMER_KEYS + "\"")
        buildConfigField("String", "TWITTER_CONSUMER_SECRET", "\"" + TWITTER_CONSUMER_SECRET + "\"")
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "unico"

    productFlavors {
        transcol {
            applicationId "com.github.ovictorpinto.meutranscol"
            signingConfig signingConfigs.configTranscol
            play {
                track = 'production'
                uploadImages = true
            }
            buildConfigField("String", "IN_LOCO_APP_ID", "\"" + TRANSCOL_IN_LOCO_APP_ID + "\"")
            buildConfigField("String", "TWITTER_USER", '"lucasromanom"')
            buildConfigField("String", "TWITTER_LIST", '"verdinho-news"')
            buildConfigField("boolean", "USA_PRECO", "true")
            manifestPlaceholders = [
                    facebookId    : "\"" + TRANSCOL_FACEBOOK_ID + "\"",
                    facebookSchema: "\"" + TRANSCOL_FACEBOOK_SCHEMA + "\""
            ]
        }
        verdinho {
            applicationId "com.github.ovictorpinto.verdinho"
            signingConfig signingConfigs.configVerdinho
            play {
                track = 'production'
                uploadImages = true
            }
            buildConfigField("String", "IN_LOCO_APP_ID", "\"" + VERDINHO_IN_LOCO_APP_ID + "\"")
            buildConfigField("String", "TWITTER_USER", '"lucasromanom"')
            buildConfigField("String", "TWITTER_LIST", '"verdinho-news"')
            buildConfigField("boolean", "USA_PRECO", "true")
            manifestPlaceholders = [
                    facebookId    : "\"" + VERDINHO_FACEBOOK_ID + "\"",
                    facebookSchema: "\"" + VERDINHO_FACEBOOK_SCHEMA + "\""
            ]
        }
        brasilia {
            applicationId "com.github.ovictorpinto.pontobrasilia"
            signingConfig signingConfigs.configBrasilia
            play {
                track = 'production'
                uploadImages = true
            }
            buildConfigField("String", "IN_LOCO_APP_ID", "\"" + BRASILIA_IN_LOCO_APP_ID + "\"")
            buildConfigField("String", "TWITTER_USER", '"ovictorpinto"')
            buildConfigField("String", "TWITTER_LIST", '"brasilia-news"')
            buildConfigField("boolean", "USA_PRECO", "false")
            manifestPlaceholders = [
                    facebookId    : "\"" + BRASILIA_FACEBOOK_ID + "\"",
                    facebookSchema: "\"" + BRASILIA_FACEBOOK_SCHEMA + "\""
            ]
        }
    }
}

def twitterApiVersion = "3.2.0"

//https://dl.google.com/dl/android/maven2/index.html
dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation(name: 'MobileSaudeAndroidLib(1.21)', ext: 'aar')
    implementation "com.android.support:design:28.0.0"
    implementation "com.android.support:cardview-v7:28.0.0"
    implementation "com.android.support:appcompat-v7:28.0.0"
    implementation "com.android.support:recyclerview-v7:28.0.0"
    implementation "com.android.support:support-v13:28.0.0"
    implementation 'com.android.support:multidex:1.0.3'
    implementation "com.google.android.gms:play-services-maps:16.0.0"
    implementation "com.google.android.gms:play-services-location:16.0.0"
    implementation "com.google.android.gms:play-services-awareness:16.0.0"
    implementation "com.google.android.gms:play-services-ads:17.0.0"
    implementation 'com.google.maps.android:android-maps-utils:0.5'
    implementation "com.google.firebase:firebase-config:16.1.0"
    implementation "com.google.firebase:firebase-core:16.0.4"
    implementation "com.google.firebase:firebase-messaging:17.3.4"
    //https://stackoverflow.com/a/43582900/2096828
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.1@aar') {
        transitive = true
    }
    implementation 'com.hendraanggrian:collapsingtoolbarlayout-subtitle:0.6.0'
    testImplementation 'junit:junit:4.12'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.2.61"
    implementation 'com.inlocomedia.android:android-sdk:3.4.+'
    //Ads and Location SDK's
    implementation 'com.facebook.android:facebook-android-sdk:[4,5)'
    implementation "com.twitter.sdk.android:twitter-core:$twitterApiVersion"
    implementation "com.twitter.sdk.android:tweet-ui:$twitterApiVersion"
}

task "publishAll"(type: GradleBuild) {
    tasks = ["assembleRelease", "publishapktranscolrelease", "publishapkverdinhorelease", "publishapkbrasiliarelease"]
}

